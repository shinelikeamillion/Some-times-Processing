class Mover {
  
  PVector location;
  PVector velocity;
  PVector acceleration;
  int topspeed = 10;
  float mass = 1.0;
  float radius = 13;
  
  Mover(float w, float h) {
    location = new PVector(w, h);
    acceleration = new PVector(0, 0);
    velocity = new PVector(0, 0);
    velocity.limit(topspeed);
  }
  
  void setRadius(float radius) {
    this.radius = radius;
    mass = radius/10;
  }
  
  void move() {
    
    velocity.add(acceleration);
    location.add(velocity);
    checkEdges();
    acceleration.mult(0);
    display();
  }
  
  void applyForce(PVector force) {
    // get() will not to change force this function deprecate
    //PVector f = force.get();
    //f.div(mass);
    
    acceleration.add(PVector.div(force, mass));
  }
  
  void display() {
    ellipse(location.x, location.y, 2*radius, 2*radius);
  }
  
  void checkEdges() {
    if (location.x > width) {
      location.x = width;
      velocity.x *= -1;
    } else if (location.x < 0) {
      location.x = 0;
      velocity.x *= -1;
    }
    if (location.y > height) {
      location.y = height;
      velocity.y *= -1;
    } else if (location.y < 0) {
      location.y = 0;
      velocity.y *= -1;
    }
  }
}
